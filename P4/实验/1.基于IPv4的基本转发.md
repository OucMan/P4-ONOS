# 基于IPv4的基本转发

本实验的目的是实现IPv4数据报的基本转发，即包括更新报文的源、目的MAC地址、更改IP首部的TTL字段，以及将数据报转发至某一端口。在本实验中，已经有了静态流表项，只需要将他们导入到交换机中即可，而不考虑控制平面下发流规则的过程，使得实验者集中考虑P4程序的编写。通过此实验，快速熟悉P4代码结构和编程风格。

## 实验拓扑

本实验的拓扑如下：

![实验拓扑]()

## P4代码解析

### 头文件以及预定义
```
#include <core.p4>
#include <v1model.p4>

const bit<16> TYPE_IPV4 = 0x800;
```

### Header

```
header ethernet_t {
    macAddr_t dstAddr;
    macAddr_t srcAddr;
    bit<16>   etherType;
}

header ipv4_t {
    bit<4>    version;
    bit<4>    ihl;
    bit<8>    diffserv;
    bit<16>   totalLen;
    bit<16>   identification;
    bit<3>    flags;
    bit<13>   fragOffset;
    bit<8>    ttl;
    bit<8>    protocol;
    bit<16>   hdrChecksum;
    ip4Addr_t srcAddr;
    ip4Addr_t dstAddr;
}

struct metadata {
    /* empty */
}

struct headers {
    ethernet_t   ethernet;
    ipv4_t       ipv4;
}
```

### Parser及校验

```
parser MyParser(packet_in packet,
                out headers hdr,
                inout metadata meta,
                inout standard_metadata_t standard_metadata) {

    state start {
        transition parse_ethernet;
    }

    state parse_ethernet {
        packet.extract(hdr.ethernet);
        transition select(hdr.ethernet.etherType) {
            TYPE_IPV4: parse_ipv4;
            default: accept;
        }
    }

    state parse_ipv4 {
        packet.extract(hdr.ipv4);
        transition accept;
    }

}

control MyVerifyChecksum(inout headers hdr, inout metadata meta) {   
    apply {  }
}


```

### Ingress

```
control MyIngress(inout headers hdr,
                  inout metadata meta,
                  inout standard_metadata_t standard_metadata) {
    action drop() {
        mark_to_drop(standard_metadata);
    }
    
    action ipv4_forward(macAddr_t dstAddr, egressSpec_t port) {
        standard_metadata.egress_spec = port;
        hdr.ethernet.srcAddr = hdr.ethernet.dstAddr;
        hdr.ethernet.dstAddr = dstAddr;
        hdr.ipv4.ttl = hdr.ipv4.ttl - 1;
    }
    
    table ipv4_lpm {
        key = {
            hdr.ipv4.dstAddr: lpm;
        }
        actions = {
            ipv4_forward;
            drop;
            NoAction;
        }
        size = 1024;
        default_action = drop();
    }
    
    apply {
        if (hdr.ipv4.isValid()) {
            ipv4_lpm.apply();
        }
    }
}

```

### Egress

```
control MyEgress(inout headers hdr,
                 inout metadata meta,
                 inout standard_metadata_t standard_metadata) {
    apply {  }
}
```


### 校验及数据包重组

```
control MyComputeChecksum(inout headers  hdr, inout metadata meta) {
     apply {
	update_checksum(
	    hdr.ipv4.isValid(),
            { hdr.ipv4.version,
	      hdr.ipv4.ihl,
              hdr.ipv4.diffserv,
              hdr.ipv4.totalLen,
              hdr.ipv4.identification,
              hdr.ipv4.flags,
              hdr.ipv4.fragOffset,
              hdr.ipv4.ttl,
              hdr.ipv4.protocol,
              hdr.ipv4.srcAddr,
              hdr.ipv4.dstAddr },
            hdr.ipv4.hdrChecksum,
            HashAlgorithm.csum16);
    }
}

control MyDeparser(packet_out packet, in headers hdr) {
    apply {
        packet.emit(hdr.ethernet);
        packet.emit(hdr.ipv4);
    }
}
```


### 函数入口


```
V1Switch(
MyParser(),
MyVerifyChecksum(),
MyIngress(),
MyEgress(),
MyComputeChecksum(),
MyDeparser()
) main;
```


### 运行

以上就是P4代码的全部，经过编译后形成json文件，然后被导入到交换机中。通过分析makefile文件，当编写完程序运行make run命令时，执行的主要操作有：
```
p4c-bm2-ss --p4v 16 --p4runtime-files build/basic.p4.p4info.txt -o build/basic.json basic.p4
```
通过该命令将我们编写的basic.p4程序编译，产生两个文件一个是basic.json文件，作为bmv2交换机的可执行文件，以及basic.p4.p4info.txt作为p4 info文件（控制器根据此文件向交换机下发流规则）。

然后执行命令：
```
python run_exercise.py -t pod-topo/topology.json -j build/basic.json -b simple_switch_grpc
```
该命令根据topology.json拓扑文件使用mininet建立拓扑，并将上一命令产生的basic.json文件导出到交换机中（在交换机初始化阶段作为参数传进去的），同时将每个交换机对应的流规则通过grpc下发至交换机。（topology.json说明了每一个交换机对应的流规则文件，p4runtime_lib.simple_controller读取对应的流规则文件，通过grpc下发至交换机）。

最后按照topology.json对主机信息MAC地址等信息的配置。

注意：本实验通过P4 Runtime实现了流规则的下发，除此之外还可以根据thrift cli的形式下发规则（只需要修改Makefile中的BMV2_SWITCH_EXE变量）。P4 Runtime控制器一侧获得的流规则文件格式如下，按照p4 info通过protobuf将流规则下发至交换机。

```
{
  "target": "bmv2",
  "p4info": "build/basic.p4.p4info.txt",
  "bmv2_json": "build/basic.json",
  "table_entries": [
    {
      "table": "MyIngress.ipv4_lpm",
      "default_action": true,
      "action_name": "MyIngress.drop",
      "action_params": { }
    },
    {
      "table": "MyIngress.ipv4_lpm",
      "match": {
        "hdr.ipv4.dstAddr": ["10.0.1.1", 32]
      },
      "action_name": "MyIngress.ipv4_forward",
      "action_params": {
        "dstAddr": "08:00:00:00:01:11",
        "port": 1
      }
    },
    {
      "table": "MyIngress.ipv4_lpm",
      "match": {
        "hdr.ipv4.dstAddr": ["10.0.2.2", 32]
      },
      "action_name": "MyIngress.ipv4_forward",
      "action_params": {
        "dstAddr": "08:00:00:00:02:22",
        "port": 2
      }
    },
    {
      "table": "MyIngress.ipv4_lpm",
      "match": {
        "hdr.ipv4.dstAddr": ["10.0.3.3", 32]
      },
      "action_name": "MyIngress.ipv4_forward",
      "action_params": {
        "dstAddr": "08:00:00:00:03:00",
        "port": 3
      }
    },
    {
      "table": "MyIngress.ipv4_lpm",
      "match": {
        "hdr.ipv4.dstAddr": ["10.0.4.4", 32]
      },
      "action_name": "MyIngress.ipv4_forward",
      "action_params": {
        "dstAddr": "08:00:00:00:04:00",
        "port": 4
      }
    }
  ]
}
```

完整项目见：

https://github.com/p4lang/tutorials/tree/master/exercises/basic


