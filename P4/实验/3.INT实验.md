# INT实验

本实验中在IP首部和以太网首部之间增加一个新的Meter首部以实现INT功能。数据包的转发根据目的IP地址，在转发的过程中，将路径上经过的交换机以及对应的端口队列长度作为遥测信息封装进Meter首部。该首部包括三部分的内容：proto_id，用来标识它内部封装的协议类型，如隧道内部的是IP报文，则proto_id等于0x0800；count标识后面遥感数据的数量；switch_t用来携带遥感信息。通信模式如下：终端将普通的数据报发送到遥测起点(Source)，然后起点为数据包添加上遥测层，然后数据报在遥测起点和遥测终点(Sink)之间进行IPv4转发，同时每经过一个转发设备都增加遥测信息到数据包，到达遥测终点后，数据报需要去掉遥测首部，然后将普通的报文在端口中转发出去，并将遥测信息发送至检测点。遥测节点将遥测信息发送至检测点的功能可以通过packet-out信息的方式发送给控制器，这个功能我们暂时不予实现。

## 实验拓扑

![实验拓扑](https://github.com/OucMan/P4-ONOS/blob/main/P4/%E5%AE%9E%E9%AA%8C/pic/1-topo.png)


## 首部信息

![数据包首部信息](https://github.com/OucMan/P4-ONOS/blob/main/P4/%E5%AE%9E%E9%AA%8C/pic/3-int.png)

## P4代码解析

### 头文件以及预定义

```
#include <core.p4>
#include <v1model.p4>

const bit<16> TYPE_METER = 0x2222;
const bit<16> TYPE_IPV4 = 0x800;

#define MAX_HOPS 9

```

### Header

```
header ethernet_t {
    macAddr_t dstAddr;
    macAddr_t srcAddr;
    bit<16>   etherType;
}

header meter_t {
    bit<16>  proto_id;
    bit<16>  count;
}

header switch_t {
    switchID_t  swid;
    qdepth_t    qdepth;
}

header ipv4_t {
    bit<4>    version;
    bit<4>    ihl;
    bit<8>    diffserv;
    bit<16>   totalLen;
    bit<16>   identification;
    bit<3>    flags;
    bit<13>   fragOffset;
    bit<8>    ttl;
    bit<8>    protocol;
    bit<16>   hdrChecksum;
    ip4Addr_t srcAddr;
    ip4Addr_t dstAddr;
}

struct ingress_metadata_t {
    bit<16>  count;
}

struct parser_metadata_t {
    bit<16>  remaining;
}

struct metadata {
    ingress_metadata_t   ingress_metadata;
    parser_metadata_t   parser_metadata;
}

struct headers {
    ethernet_t         ethernet;
    meter_t            meter;
    switch_t[MAX_HOPS] swtraces;
    ipv4_t             ipv4;
}

```

### Parser及校验
```
parser MyParser(packet_in packet,
                out headers hdr,
                inout metadata meta,
                inout standard_metadata_t standard_metadata) {

    state start {
        transition parse_ethernet;
    }

    state parse_ethernet {
        packet.extract(hdr.ethernet);
        transition select(hdr.ethernet.etherType) {
            TYPE_METER: parse_meter;
            TYPE_IPV4: parse_ipv4;
            default: accept;
        }
    }

    state parse_ipv4 {
        packet.extract(hdr.ipv4);
        transition accept;
    }

    state parse_meter {
        packet.extract(hdr.meter);
        meta.parser_metadata.remaining = hdr.meter.count;
        transition select(meta.parser_metadata.remaining) {
            0 : parse_ipv4;
            default: parse_swtrace;
        }
    }

    state parse_swtrace {
        packet.extract(hdr.swtraces.next);
        meta.parser_metadata.remaining = meta.parser_metadata.remaining  - 1;
        transition select(meta.parser_metadata.remaining) {
            0 : parse_ipv4;
            default: parse_swtrace;
        }
    }    
}

control MyVerifyChecksum(inout headers hdr, inout metadata meta) {   
    apply {  }
}

```

### Ingress

Ingress流水线中代码的功能为：根据目的IP地址来指定输出端口，然后更新Meter首部，注意这里只是为遥测信息开辟一个空间，具体的赋值需要在Egress中实现。

```
control MyIngress(inout headers hdr,
                  inout metadata meta,
                  inout standard_metadata_t standard_metadata) {
    action drop() {
        mark_to_drop(standard_metadata);
    }
    
    action meter_forward(macAddr_t dstAddr, egressSpec_t port) {
        standard_metadata.egress_spec = port;
        hdr.ethernet.srcAddr = hdr.ethernet.dstAddr;
        hdr.ethernet.dstAddr = dstAddr;
        hdr.ipv4.ttl = hdr.ipv4.ttl - 1;
        
        if (!hdr.meter.isValid()){
            hdr.ethernet.etherType = TYPE_METER;
            hdr.meter.setValid();
            hdr.meter.proto_id = TYPE_IPV4;
            hdr.meter.count = 0;
        }
        hdr.meter.count = hdr.meter.count + 1;
        hdr.swtraces.push_front(1);
        hdr.swtraces[0].setValid();
    }

    table ipv4_lpm {
        key = {
            hdr.ipv4.dstAddr: lpm;
        }
        actions = {
            meter_forward;
            drop;
            NoAction;
        }
        size = 1024;
        default_action = NoAction();
    }
    
    apply {
        if (hdr.ipv4.isValid()) {
            ipv4_lpm.apply();
        }
    }
}

```

### Egress

```
control MyEgress(inout headers hdr,
                 inout metadata meta,
                 inout standard_metadata_t standard_metadata) {
    action add_swtrace(switchID_t swid) { 
        hdr.swtraces[0].swid = swid;
        hdr.swtraces[0].qdepth = (qdepth_t)standard_metadata.deq_qdepth;
    }

    table swtrace {
        actions = { 
	          add_swtrace; 
	          NoAction; 
        }
        default_action = NoAction();      
    }
    
    action m_egress() {
        // After saving INT information, drop their fields
        hdr.ethernet.etherType = TYPE_IPV4;
        hdr.meter.setInvalid();
        hdr.swtraces.setInvalid();
    }
    
    table meter_egress {
        key = {
            hdr.ipv4.dstAddr: lpm;
            hdr.swtraces[0].swid: exact;
        }
        actions = { 
	          m_egress; 
	          NoAction; 
        }
        default_action = NoAction(); 
        
    }
    
    apply {
        if (hdr.meter.isValid()) {
            swtrace.apply();
            meter_egress.apply();
        }
    }
}
```

### 校验及数据包重组

```
control MyComputeChecksum(inout headers  hdr, inout metadata meta) {
     apply {
	update_checksum(
	    hdr.ipv4.isValid(),
            { hdr.ipv4.version,
	      hdr.ipv4.ihl,
              hdr.ipv4.diffserv,
              hdr.ipv4.totalLen,
              hdr.ipv4.identification,
              hdr.ipv4.flags,
              hdr.ipv4.fragOffset,
              hdr.ipv4.ttl,
              hdr.ipv4.protocol,
              hdr.ipv4.srcAddr,
              hdr.ipv4.dstAddr },
            hdr.ipv4.hdrChecksum,
            HashAlgorithm.csum16);
    }
}

control MyDeparser(packet_out packet, in headers hdr) {
    apply {
        packet.emit(hdr.ethernet);
        packet.emit(hdr.meter);
        packet.emit(hdr.swtraces);   
        packet.emit(hdr.ipv4);              
    }
}
```

### 函数入口

```
V1Switch(
MyParser(),
MyVerifyChecksum(),
MyIngress(),
MyEgress(),
MyComputeChecksum(),
MyDeparser()
) main;
```

### 运行

```
像上一个实验那样，当编写完P4程序后直接运行make run命令即可，该命令的细节，见上一实验。
```




